buildscript {
    repositories {
        mavenCentral()
    }
    configurations.classpath {
        resolutionStrategy.activateDependencyLocking()
    }
}

plugins {
    id 'java'
    id 'checkstyle'
    id 'groovy'
}

apply plugin: 'java'

task buildSimulatorLibrary(type: Exec) {
    group "Build"
    description "Build Rust simulator library"
    workingDir 'src/test/rust/simulator'

    commandLine 'cargo', 'build'
}

tasks.withType(Test) {
    systemProperty "java.library.path", "../../rust/simulator/target/debug/"
}

checkstyleMain {
    source ='src/main/java'
    maxWarnings = 0
}

checkstyleTest {
    source ='src/test/java'
    maxWarnings = 0
}


repositories {
    mavenCentral()
}

configurations {
    javacardAntTask
}

dependencies {
    testImplementation (
        'org.junit.jupiter:junit-jupiter-engine:latest.release',
        'com.klinec:jcardsim:latest.release'
    )
    implementation (
        'com.klinec:jcardsim:latest.release'
    )
    javacardAntTask 'com.github.martinpaljak:ant-javacard:latest.release'
}

dependencyLocking {
    lockAllConfigurations()
}

test {
    dependsOn buildSimulatorLibrary
    workingDir 'src/test/java/emvcardsimulator'

    useJUnitPlatform()

    testLogging {
        outputs.upToDateWhen { false }
        events "started", "skipped", "failed"
        exceptionFormat "full"
        showStandardStreams true
    }

    failFast = false
}

compileJava {
    options.compilerArgs = [
            '-Xlint:all',
            '-Xlint:-options', //java 13 warns about Java 7 targetting
            '-Xlint:-deprecation', // Some Java Card features are deprecated in 3.0.5 but we target earlier versions
            '-Werror'
    ]
    // // JavaCard 3.0.5 supports Java 1.7 class files (class version 51)
    // // JavaCard 3.0.3-3.0.4 supports Java 1.6 class files (class version 50)
    // // JavaCard 2.2.2 supports Java 1.5 class files (class version 49)
    // sourceCompatibility = 1.6
    // targetCompatibility = 1.6
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

compileTestJava {
    options.compilerArgs = [
            '-Xlint:all',
            '-Xlint:-options', //java 13 warns about Java 7 targetting
            '-Werror'
    ]
    // JUnit 5 requires Java 8 or newer
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

task downloadGp {
    doLast {
        String gpJar = 'gp.jar'
        if (!new File(gpJar).exists()) {
            String checksumExtension = '.sha512.tmp'
            String gpJarTemp = gpJar + checksumExtension

            ant.get(src: 'https://github.com/martinpaljak/GlobalPlatformPro/releases/download/v20.07.04/gp.jar', dest:gpJar)

            ant.checksum(file: gpJar, algorithm:'SHA-512', fileext: checksumExtension)
            String expectedChecksum = "4030c6eb46fb932c167f9a53fdbfab6b1364dbaafad5068b5bfc9779140cc1659e655d5ff53ee20d2823163011ede3ec9c3916632c2173cb6a1e17b84389490a"
            String actualChecksum = new File(gpJarTemp).text.trim()
            delete gpJarTemp

            if (!expectedChecksum.equals(actualChecksum)) {
                throw new GradleException("Checksum not valid! file: " + gpJar + ", expected: " + expectedChecksum + ", actual: " + actualChecksum)        
            }
        }
    }
}

task cap {
    dependsOn assemble
    group "Build"
    description "Assembles a cap archive containing the main classes."

    doLast {
        ant.taskdef(name: 'javacard', classname: 'pro.javacard.ant.JavaCard', classpath: configurations.javacardAntTask.asPath)

        // 1PAY.SYS.DDF01
        ant.javacard(jckit: 'oracle_javacard_sdks/jc305u3_kit') {
            cap (
                package: 'emvcardsimulator',
                version: '0.1',
                output: 'build/pse.cap',
                jca: 'build/pse.jca',
                export: 'build',
                classes: "build/classes/java/main",
                aid: '315041592E000000000000000000') {
                    applet (class: 'emvcardsimulator.PaymentSystemEnvironment', aid: '315041592E5359532E4444463031')
            }
        }

        ant.javacard(jckit: 'oracle_javacard_sdks/jc305u3_kit') {
            cap (
                package: 'emvcardsimulator',
                version: '0.1',
                output: 'build/paymentapp.cap',
                jca: 'build/paymentapp.jca',
                export: 'build',
                classes: "build/classes/java/main",
                aid: 'AFFFFFFFFF0000') {
                    applet (class: 'emvcardsimulator.PaymentApplication', aid: 'AFFFFFFFFF1234')
            }
        }
    }
}

task deployPse(type: Exec) {
    dependsOn downloadGp, cap
    group "JavaCard"
    description "Deploy PaymentSystemEnvironment applet to JavaCard"

    commandLine 'java', '-jar', 'gp.jar', '--force', '--install', 'build/pse.cap'
}

task deployPaymentApp(type: Exec) {
    dependsOn downloadGp, cap
    group "JavaCard"
    description "Deploy payment application applet to JavaCard"

    commandLine 'java', '-jar', 'gp.jar', '--force', '--install', 'build/paymentapp.cap'
}
